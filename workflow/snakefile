"""
snakemake -n
snakemake -c 1 
snakemake --rulegraph | dot -Tsvg > dag_process.svg
"""
import datetime

SCFS = [ "hap_1_id1", "hap_2_id1" ]

rule all:
    input:
      vcf = "results/vcf/genotpyes.vcf.gz"

rule vcf_header:
  input:
    vcf = "data/proto.vcf",
    genome = "data/sep_chr_1.genome"
  output:
    vcf = "results/vcf/header.vcf"
  params:
    date = datetime.datetime.today().strftime('%Y%m%d'),
    reference = "sep_chr_1.fa",
    species = '\\"dummy\\"'
  shell:
    """
    sed 's/XXgenomeXX/{params.reference}/; s/XXdateXX/{params.date}/' {input.vcf} > {output.vcf}
    awk '{{print "##contig=<ID="$1",length="$2",species={params.species}>"}}' {input.genome} >> {output.vcf}
    echo '##FORMAT=<ID=GT,Number=1,Type=String,Description="Genotype">' >> {output.vcf}
    """

rule convert_scaf_vcf:
    input:
      tsv = "data/{mscaf}.tsv"
    output:
      vcf = temp( "results/vcf/{mscaf}.vcf" )
    shell:
      """
      Rscript --vanilla R/snps_to_vcf.R {input.tsv} {output.vcf}
      """

rule combine_vcfs:
    input: 
      vcf_header = "results/vcf/header.vcf",
      vcf_body = expand( "results/vcf/{mscaf}.vcf", mscaf = SCFS )
    output:
      vcf = "results/vcf/genotpyes.vcf.gz"
    params:
      prefix = "results/vcf/genotpyes.vcf"
    shell:
      """
      cat {input.vcf_header} > {params.prefix}
      head -n 1 {input.vcf_body[0]} >> {params.prefix}
      tail -n +2 {input.vcf_body} | grep -v "==>" | grep -v "^$" >> {params.prefix}
      gzip {params.prefix}
      """